import com.github.jk1.license.filter.*

plugins {
	id 'org.openapi.generator' version '7.0.0' apply false
	id 'com.google.cloud.tools.jib' version '3.2.0' apply false
	id 'org.sonarqube' version '4.3.1.3277' apply false
	id 'com.diffplug.spotless' version '6.12.1' apply false
	id 'org.owasp.dependencycheck' version '8.0.1' apply false
	id 'com.github.spotbugs' version '5.0.13' apply false
	id 'com.github.jk1.dependency-license-report' version '2.5'
}


licenseReport {
	allowedLicensesFile = new File("$projectDir/config/allowed-licenses.json")
	filters = [new LicenseBundleNormalizer(bundlePath: "$projectDir/config/license-normalizer-bundle.json")]
	excludeBoms = true
	excludes = [
			// Packages only brings in additional dependencies that are evaluated individually
			'org.springframework.boot:spring-boot-dependencies',
			'com.google.guava:guava-parent'
	]
}

project.tasks.named("checkLicense") {
	inputs.file(licenseReport.allowedLicensesFile)
			.withPropertyName("allowedLicensesConfiguration")
}

task("check") {
	dependsOn("checkLicense")
}

allprojects {
	group = 'io.nuvalence.ds4g.documentmanagement'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
		maven {
			url 'https://oss.sonatype.org/service/local'
		}
		maven {
			url 'https://oss.sonatype.org/content/repositories/snapshots/'
		}
	}
}


